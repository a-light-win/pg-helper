import "../lib/e2e.just"

[no-cd,private]
test-all: _all_secrets \
  (_venom 'podman' 'file-source')

# run a specific test suite
[no-cd]
test testsuit compose_provider='podman': _all_secrets (_venom compose_provider testsuit)

# clean up files generated by the e2e tests
[no-cd]
clean: _e2e_clean_all

# list all e2e test suites
[no-cd]
testsuites:
  #!/usr/bin/env bash
  find e2e/tests -maxdepth 1 -name '*.yaml' -exec basename {} .yaml \;

# install and update venom to the latest version
[no-cd]
update-venom: _update_venom

# Show the logs of a specific test suite
[no-cd]
log name compose_provider='podman':
  cat e2e/output/{{ compose_provider }}-{{ name }}/venom.log

# Show the test results of a specific test suite
[no-cd]
result name compose_provider='podman':
  cat e2e/output/{{ compose_provider }}-{{ name }}/test_results_tests_{{ name }}.xml
